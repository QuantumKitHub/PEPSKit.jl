{
 "cells": [
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Markdown #hide"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "# C₄ᵥ CTMRG and QR-CTMRG\n",
    "\n",
    "In this example we demonstrate specialized CTMRG variants that exploit the $C_{4v}$ point group\n",
    "symmetry of the physical model and PEPS at hand. This allows us to significantly reduce the\n",
    "computational cost of contraction and optimization. To that end, we will consider the Heisenberg\n",
    "Hamiltonian on the square lattice\n",
    "\n",
    "$$\n",
    "H = \\sum_{\\langle i,j \\rangle} \\left ( J_x S^{x}_i S^{x}_j + J_y S^{y}_i S^{y}_j + J_z S^{z}_i S^{z}_j \\right )\n",
    "$$\n",
    "\n",
    "We want to treat the model in the antiferromagnetic regime where the ground state exhibits\n",
    "bipartite sublattice structure. To be able to represent this ground state in a $C_{4v}$-invariant\n",
    "manner on a single-site unit cell, we perform a unitary sublattice rotation by setting\n",
    "$(J_x, J_y, J_z)=(-1, 1, -1)$.\n",
    "\n",
    "Let's get started by seeding the RNG and doing the imports:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Random\n",
    "using TensorKit, PEPSKit\n",
    "Random.seed!(123456789);"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Defining a specialized Hamiltonian for C₄ᵥ-symmetric PEPS\n",
    "\n",
    "Since the model under consideration and its ground state are invariant under 90° rotation and\n",
    "Hermitian reflection, evaluating the expectation values of the horizontal and vertical energy\n",
    "contributions are exactly equivalent. This allows us to effectively halve the computational cost\n",
    "by evaluating only half of the terms and multiplying by 2. In practice, we implement this\n",
    "using a specialized `LocalOperator` that contains only the relevant terms:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using MPSKitModels: S_xx, S_yy, S_zz\n",
    "\n",
    "# Heisenberg model assuming C4v symmetric PEPS and environment, which only evaluates necessary term\n",
    "function heisenberg_XYZ_c4v(lattice::InfiniteSquare; kwargs...)\n",
    "    return heisenberg_XYZ_c4v(ComplexF64, Trivial, lattice; kwargs...)\n",
    "end\n",
    "function heisenberg_XYZ_c4v(\n",
    "        T::Type{<:Number}, S::Type{<:Sector}, lattice::InfiniteSquare;\n",
    "        Jx = -1.0, Jy = 1.0, Jz = -1.0, spin = 1 // 2,\n",
    "    )\n",
    "    @assert size(lattice) == (1, 1) \"only trivial unit cells supported by C4v-symmetric Hamiltonians\"\n",
    "    term =\n",
    "        rmul!(S_xx(T, S; spin = spin), Jx) +\n",
    "        rmul!(S_yy(T, S; spin = spin), Jy) +\n",
    "        rmul!(S_zz(T, S; spin = spin), Jz)\n",
    "    spaces = fill(domain(term)[1], (1, 1))\n",
    "    return LocalOperator( # horizontal and vertical contributions are identical\n",
    "        spaces, (CartesianIndex(1, 1), CartesianIndex(1, 2)) => 2 * term\n",
    "    )\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Initializing C₄ᵥ-invariant PEPSs and environments\n",
    "\n",
    "In order to use $C_{4v}$-symmetric algorithms, it is of course crucial to use initial guesses\n",
    "with $C_{4v}$ symmetry. First, we create a real-valued random PEPS that we explicitly\n",
    "symmetrize using `symmetrize!` and the $C_{4v}$ symmetry `RotateReflect`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "symm = RotateReflect()\n",
    "D = 2\n",
    "T = Float64\n",
    "peps_random = InfinitePEPS(randn, T, ComplexSpace(2), ComplexSpace(D))\n",
    "peps₀ = symmetrize!(peps_random, symm);"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Initializing an $C_{4v}$-invariant environment is a bit more subtle and there is no one-size-fits-all\n",
    "solution. As a good starting point one can use the initialization function `initialize_random_c4v_env`\n",
    "(or also `initialize_singlet_c4v_env`) where we construct a diagonal corner with random\n",
    "real entries and a random Hermitian edge tensor."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "χ = 16\n",
    "env_random_c4v = initialize_random_c4v_env(peps₀, ComplexSpace(χ));"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Then contracting the PEPS using $C_{4v}$ CTMRG is as easy as just calling `leading_boundary`\n",
    "but passing the initial PEPS and environment as well as the `alg = :c4v` keyword argument:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "env₀, = leading_boundary(env_random_c4v, peps₀; alg = :c4v, tol = 1.0e-10);"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## C₄ᵥ-symmetric optimization\n",
    "\n",
    "We now take `peps₀` and `env₀` as a starting point for a gradient-based energy\n",
    "minimization where we contract using $C_{4v}$ CTMRG such that the energy gradient will also\n",
    "exhibit $C_{4v}$ symmetry. For that, we call `fixedpoint` and specify `alg = :c4v`\n",
    "as the boundary contraction algorithm:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "H = real(heisenberg_XYZ_c4v(InfiniteSquare())) # make Hamiltonian real-valued\n",
    "peps, env, E, = fixedpoint(\n",
    "    H, peps₀, env₀; optimizer_alg = (; tol = 1.0e-4), boundary_alg = (; alg = :c4v),\n",
    ");"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We note that this energy is slightly higher than the one obtained from an\n",
    "optimization using asymmetric CTMRG with equivalent settings.\n",
    "Indeed, this is what one would expect since the $C_{4v}$ symmetry restricts the PEPS ansatz\n",
    "leading to fewer free parameters, i.e. an ansatz with reduced expressivity.\n",
    "Comparing against Juraj Hasik's data from $J_1\\text{-}J_2$\n",
    "[PEPS simulations](https://github.com/jurajHasik/j1j2_ipeps_states/blob/main/single-site_pg-C4v-A1/j20.0/state_1s_A1_j20.0_D2_chi_opt48.dat),\n",
    "we find very good agreement:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "E_ref = -0.6602310934799577 # Juraj's energy at D=2, χ=16 with C4v symmetry\n",
    "@show (E - E_ref) / E_ref;"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "As a consistency check, we can compute the vertical and horizontal correlation lengths,\n",
    "and should find that they are equal (up to the sparse eigensolver tolerance):"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "ξ_h, ξ_v, = correlation_length(peps, env)\n",
    "@show ξ_h ξ_v;"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## QR-CTMRG\n",
    "\n",
    "The conventional $C_{4v}$ CTMRG algorithm works by performing an eigendecomposition of\n",
    "the enlarged corner $A = V D V^\\dagger$, taking the diagonal eigenvalue tensor as the new\n",
    "corner $C' = D$ and then renormalizing the edge using the isometry $V$. There exist modifications\n",
    "to this standard algorithm, most notably *QR-CTMRG* as presented in a recent publication by\n",
    "[Zhang, Yang and Corboz](@cite zhang_accelerating_2025). There the idea is to replace the\n",
    "eigendecomposition by a QR decomposition of a lower-rank approximation of the enlarged corner.\n",
    "While less accurate in some ways, the QR-CTMRG approach substantially accelerates contraction\n",
    "and optimization times, and also has vastly improved GPU performance. Notably, it is found\n",
    "that QR-CTMRG converges to the same fixed point as regular $C_{4v}$ CTMRG.\n",
    "\n",
    "In PEPSKit terms, using QR-CTMRG just amounts to switching out the projector algorithm that is\n",
    "used by the `C4vCTMRG` algorithm to `projector_alg = :c4v_qr` (as opposed to `:c4v_eigh`).\n",
    "QR-CTMRG tends to need significantly more iterations to converge while still being much faster,\n",
    "hence we need to increase `maxiter`:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "env_qr₀, = leading_boundary(\n",
    "    env_random_c4v, peps; alg = :c4v, projector_alg = :c4v_qr, maxiter = 500,\n",
    ");"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "To optimize using QR-CTMRG we proceed analogously by specifiying `projector_alg = :c4v_qr` and\n",
    "increasing the `maxiter` when setting the boundary algorithm parameters. We make sure to supply\n",
    "the `env_qr₀` initial environment because it does not use `DiagonalTensorMap`s as its corner\n",
    "type (only regular `eigh`-based $C_{4v}$ CTMRG produces diagonal corners):"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "peps_qr, env_qr, E_qr, = fixedpoint(\n",
    "    H, peps₀, env_qr₀;\n",
    "    optimizer_alg = (; tol = 1.0e-4),\n",
    "    boundary_alg = (; alg = :c4v, projector_alg = :c4v_qr, maxiter = 500),\n",
    "    gradient_alg = (; alg = :linsolver)\n",
    ");\n",
    "@show (E_qr - E_ref) / E_ref;"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  },
  "kernelspec": {
   "name": "julia-1.11",
   "display_name": "Julia 1.11.7",
   "language": "julia"
  }
 },
 "nbformat": 4
}